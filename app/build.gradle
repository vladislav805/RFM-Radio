apply plugin: 'com.android.application'

def versionMajor = 0
def versionMinor = 6
def versionPatch = 0

def dev = false

android {
    compileSdkVersion 28

    defaultConfig {
        //noinspection GroovyAssignabilityCheck
        applicationId dev ? "com.vlad805.fmradio.dev" : "com.vlad805.fmradio"
        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion
        targetSdkVersion 22
        //noinspection GroovyAssignabilityCheck
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        //noinspection GroovyAssignabilityCheck
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
    }

    buildTypes {
        release {
            //noinspection GroovyAssignabilityCheck
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    //noinspection GroovyMissingReturnStatement
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    productFlavors {
    }

    //noinspection GroovyMissingReturnStatement
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${defaultConfig.applicationId}-${variant.versionName}.apk"
        }
    }

}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == "com.android.support") {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion "26.+"
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.core:core:1.6.0-rc01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3' // AppCompat does not support the current theme features
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.annotation:annotation:1.2.0'
    implementation 'androidx.preference:preference:1.1.0' // broke apk
    implementation 'androidx.media:media:1.1.0' // broke build
    implementation 'com.github.naman14:TAndroidLame:1.1'
    implementation 'com.github.atsushi-ageet:tray:0.14.0'
}
